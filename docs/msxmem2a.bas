110 REM MSXMEM v2.03 - Aanvullingen door Olympiasoft PD
120 DEFINTA-Z:COLOR15,4,4:SCREEN2:OPEN"GRP:"AS#1
130 DIMF1(16)
140 ' FUNCTIES SCHERM OPBOUW
150 DEFFNX(A,B)=20-140*(A>=2)+B*20
160 DEFFNY(A,B)=10-76*(((A=1)+(A=3))<>0)
170 DEFFNXX(M)=FNX(MAND3,(MAND12)/4)+5
180 DEFFNYY(M)=FNY(MAND3,(MAND12)/4)+4
190 'BOUW SCHERM
200 FORT=0TO3
210 FORE=0TO3
220 PRESET(FNX(T,0)-10,5+15*E+FNY(T,U))
230 PRINT#1,RIGHT$(STR$(3-E),1)
240 NEXTE
250 FORU=0TO3
260 A=FNX(T,U):B=FNY(T,U)
270 PRESET(A+2,B-9):PRINT#1,RIGHT$(STR$(T),1);RIGHT$(STR$(U),1)
280 DRAW"BM=A;,=B;R15D15NL15D15NL15D15NL15D15L15U60"
290 NEXTU
300 NEXTT
310 GOSUB1590
320 'BOIS-ROM
330 PRESET(FNX(0,0)+6,FNY(0,0)+3*15+5)
340 PRINT#1,"B"
350 'MSX1-ROM
360 M=PEEK(&HFCC1)
370 PRESET(FNX(0,(MAND12)/4)+5,FNY(0,(MAND12)/4)+2*15+4)
380 PRINT#1,"1"
390 'MSX2(+)-ROM (MSX2+ SUB-ROM part 1/3)
400 IFPEEK(&H2D)=0THEN460
410 M=PEEK(&HFAF8)
420 IFM=0THEN460
423 PL$="":PL=0:IFPEEK(&H2D)>1THENPL$="+":PL=5
430 PRESET(FNX(MAND3,(MAND12)/4)+6-PL,FNY(MAND3,(MAND12)/4)+3*15+4)
440 PRINT#1,"2"+PL$
441 'KANJI-ROM (MSX2+ SUB-ROM part 2/3)
442 IFPEEK(&H2D)<2THEN460
443 M=PEEK(&HFAF8)
444 PRESET(FNX(MAND3,(MAND12)/4)+5,FNY(MAND3,(MAND12)/4)+2*15+4)
445 PRINT#1,"K"
446 'KANJI-ROM (MSX2+ SUB-ROM part 3/3)
447 IFPEEK(&H2D)<2THEN460
448 M=PEEK(&HFAF8)
449 PRESET(FNX(MAND3,(MAND12)/4)+5,FNY(MAND3,(MAND12)/4)+1*15+4):PRINT#1,"K"
450 'DISK(S) ROM
460 IFPEEK(&HFFA7)=&HC9THEN540
470 FORT=&HFB22TO&HFB28STEP&H2
480 DN=DN+1
490 IFPEEK(T)=0THEN530
500 M=PEEK(T)
510 PRESET(FNXX(M)-4,FNYY(M)+2*15)
520 PRINT#1,"D";:DRAW"BL9":PRINT#1,PEEK(T-1)
530 NEXTT
540 'SLOTSELECT REGISTER
550 RESTORE1790
560 FORT=&HB000TO&HB007
570 READA$
580 POKET,VAL("&H"+A$)
590 NEXTT
600 DEFUSR=&HB000:DU=USR(0)
610 SL=PEEK(&HAFFF):T=128:Y=0:P=SL
620 IFT<1THEN660
630 Q=P\T
640 IFQ>=1THENA(Y)=1:P=P-TELSEA(Y)=0
650 Y=Y+1:T=T/2:GOTO620
660 FORP=0TO3
670 S=A(P*2)*2+A(P*2+1)
680 IFPEEK(&HFCC1+S)<>&H80THENE=0:GOTO700
690 E=(PEEK(&HFCC5+S)AND(4^(3-P)*3))/(4^(3-P))
700 A1=FNX(S,E)+2:A2=FNY(S,E)+P*15+2
710 LINE(A1,A2)-(A1+11,A2+11),,B
720 NEXTP
730 P=O:S=0:E=0
740 'RAM PAGE 3
750 RESTORE1800
760 FORT=&HB000TO&HB040
770 READA$
780 POKET,VAL("&H"+A$)
790 NEXTT
800 DEFUSR=&HB000
810 PA=(SLAND&HC0)/64
820 IFPEEK(&HFCC1+PA)=0THEN840
830 PA=PA+(PEEK(&HFCC5+PA)AND&HC0)/16+&H80
840 POKE&HB036,PA
850 FORT=0TO15
860 POKE&HAFFF,&HFF
870 POKE&HB011,PEEK(&HFCC1+(T AND 3))+T
880 DU=USR(0)
890 IFPEEK(&HAFFF)=255THEN940
900 M=PEEK(&HAFFF)
910 IFPEEK(&HFCC1+(T AND 3))=0THENM=M AND 3
920 PRESET(FNXX(M),FNYY(M))
930 PRINT#1,"R"
940 NEXTT
950 'RAM PAGE 0-1-2
960 Z=1:RESTORE1820
970 FORT=&HB000TO&HB023
980 READA$
990 POKET,VAL("&H"+A$)
1000 NEXTT
1010 DEFUSR=&HB000
1020 FORY=1TO3
1030 FORT=0TO15
1040 POKE&HB002,Y*64-1
1050 POKE&HAFFF,&HFF:POKE&HB004,PEEK(&HFCC1+(T AND 3))+T
1060 DU=USR(0)
1070 IFPEEK(&HAFFF)=255THEN1130
1080 M=PEEK(&HAFFF)
1090 IFPEEK(&HCC1+(T AND 3))=0THENM=M AND 3
1100 IFY=2ANDF1(Z-1)<>MTHENF1(Z)=M:Z=Z+1
1110 PRESET(FNXX(M),FNYY(M)+(4-Y)*15)
1120 PRINT#1,"R"
1130 NEXTT
1140 NEXTY
1150 'CARTRIDGE OF INIT FIRMWARE
1160 FORS=0TO3
1170 FORE=0TO12STEP4
1180 FORP=0TO3
1190 IFPEEK(S*16+E+P+&HFCC9)=0THEN1260
1200 M=S+E
1210 FORT=0TO7
1220 IFPOINT(FNXX(M)+T-2,FNYY(M)+(3-P)*15+4)<>PEEK(&HF3EA)THEN1260
1230 NEXTT
1240 PRESET(FNXX(M),FNYY(M)+(3-P)*15)
1250 PRINT#1,"C"
1260 NEXTP
1270 NEXTE
1280 NEXTS
1290 'MEMORY MAPPER
1300 IFPEEK(&H2D)=0THEN1510
1310 OUT&HFD,2:OUT&HFC,3:IFINP(&HFD)=INP(&HFC)THEN1510
1320 POKE&HAFFF,0:RESTORE1830
1330 FORT=&HB000TO&HB051
1340 READD$
1350 POKET,VAL("&H"+D$)
1360 NEXTT
1370 DEFUSR=&HB000
1380 FORT=1TOZ-1
1390 POKE&HB001,F1(T)
1400 POKE&HB009,SL+((F1(T)AND3)*4)
1410 POKE&HB04C,SL
1420 DU=USR(0)
1430 AB=PEEK(&HAFFF)
1440 IFAB<>0THENSM=F1(T):GOTO1470
1450 NEXTT
1460 'MELDT MEMO MAPPER
1470 TX$=STR$(AB*16)+" Kb"
1480 XT=95:YT=100:GOSUB1720:TX$="MM RAM":XT=100:YT=110:GOSUB1720
1490 TX$="in slot":XT=98:YT=120:GOSUB1720:TX$=STR$(SMAND3)+STR$(-(SMAND&HC)/4):XT=102:YT=130:GOSUB1720
1500 'PROGRAMMA GEGEVENS
1510 TX$="MSXMEM":XT=100:YT=0:GOSUB1720
1520 TX$="V2.03":XT=104:YT=8:GOSUB1720
1530 TX$="door":XT=108:YT=16:GOSUB1720
1540 TX$="Armand":XT=100:YT=32:GOSUB1720
1550 TX$="Simonis":XT=96:YT=40:GOSUB1720
1551 TX$="i.s.m.":XT=100:YT=51:GOSUB1720
1552 TX$="Albert":XT=100:YT=64:GOSUB1720
1553 TX$="Beevendorp":XT=92:YT=72:GOSUB1720
1560 BEEP
1570 GOTO1570
1580 'MSX TYPE GEGEVENS
1590 RESTORE1770
1600 FORT1=0TO2
1610 READTX$
1620 FORT=1TOLEN(TX$)
1630 PRESET(5+T*6,149+T1*8):PRINT#1,MID$(TX$,T,1)
1640 NEXTT
1650 NEXTT1
1660 I=PEEK(&H2B)AND15:TX$="TEKENSET:"+MID$("JAPINTKOR",1+I*3,3):XT=5:YT=173:GOSUB1720
1670 I=(PEEK(&H2B)AND112)/16:TX$="DATUM:"+MID$("YY-MM-DDMM-DD-YYDD-MM-YY",1+I*8,8):XT=87:YT=173:GOSUB1720
1680 I=(PEEK(&H2C)AND240)/16:TX$="BASIC:"+MID$("JAPINT",1+I*3,3):XT=178:YT=173:GOSUB1720
1685 IFPEEK(&H2D)THENI=((VDP(10)AND2)/2)ELSEI=((PEEK(&H2B)AND128)/128)
1690 TX$="INTERRUPT:"+MID$("6050",1+I*2,2)+" Hz":XT=5:YT=181:GOSUB1720
1700 I=PEEK(&H2C)AND15:TX$="TOETSENBORD:"+MID$("JAPINTFRAENGDINKORDENNORZWESPARUS",1+I*3,3):XT=103:YT=181:GOSUB1720
1710 RETURN
1720 FORT=1TOLEN(TX$)
1730 PRESET(XT+6*T,YT):PRINT#1,MID$(TX$,T,1)
1740 NEXTT
1750 RETURN
1760 'DATA
1770 DATA ROMS=> B=BIOS  1=MSX1  2=MSX2  2+=MSX2+, K=KANJI  Dx=DISK+aantal drives  R=RAM, C=Cartridge OF GEINITIALISEERDE firmware
1780 'MACHINE CODE
1790 DATA CD,38,01,32,FF,AF,FB,C9
1800 DATA F3,ED,73,0,B8,31,0,AF,CD,38,1,F5,21,0,D0,E5,3E,8F,CD,24,0,E1,7E,2F,77,AE,CA,24,B0,F1,CD,3B,1
1810 DATA C3,32,B0,3A,11,B0,32,FF,AF,F1,CD,3B,1,0,0,0,0,21,0,D0,3E,0,CD,24,0,ED,7B,0,B8,FB,C9,FF
1820 DATA 21,FF,3F,3E,83,F5,CD,C,0,2F,5F,F1,F5,CD,14,0,F1,F5,D5,CD,C,0,D1,AB,CA,1E,B0,F1,FB,C9,F1,32,FF,AF,FB,C9
1830 DATA 3E,3,21,0,40,CD,24,0,3E,FC,CD,3B,1,3E,2,D3,FD,21,0,40,36,58,3C,D3,FD,3A,0,40,EE,58,C2,25,B0,AF,C3,3B,B0,3E,06
1840 DATA D3,FD,21,0,40,4E,F5,79,EE,58,28,5,F1,C6,4,18,EF,F1,D6,2,32,FF,AF,3E,2,D3,FD,3A,C1,FC,21,0,40,CD,24,0,3E,F0,CD,3B,1,FB,C9
